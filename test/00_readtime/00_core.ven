nud pad(symbol) {
  <{
    # These ensures are run at read-time.

    # Make sure homoiconics evaluate.
    ensure 12;
    ensure "12";
    ensure `12`;
    ensure true;
    ensure not false;

    # Make sure vectors evaluate.
    ensure [1, 2, 3];
    ensure [1, "2", 3];
    ensure [[1, 2, 3], 4, 5];

    # Make sure we can access symbols.
    # value check todo
    ensure symbol is 12;

    # Make sure unaries evaluate [same way as in runtime]:
    ensure +1 is 1;
    ensure -1 is -1;
    ensure ~"hello" is "hello";
    ensure &[1] is [1];
    ensure #[1, 2, 3] is 3;
    ensure #"hello" is 5;

    ensure +"1" is 1;
    ensure +[2, 4, 6] is 3;
    ensure +true is 1;
    ensure +false is 0;

    ensure -"1" is -1;
    ensure -[2, 4, 6] is -3;
    ensure -true is -1;
    ensure -false is 0;

    ensure ~123 is "123";
    ensure ~[1, 2, 3] is "[1, 2, 3]";
    ensure ~true is "true";
    ensure ~false is "false";

    ensure &1 is [1];
    ensure &"1" is ["1"];
    ensure &true is [true];
    ensure &false is [false];

    ensure #123 is 3;
    ensure #true is 4;
    ensure #false is 5;

    # Make sure normal binaries evaluate [same way as in runtime]:
    ensure 1 < 2;
    ensure 2 > 1;
    ensure 2 <= 2;
    ensure 5 >= 4;

    ensure 1 + 1 is 2;
    ensure 2 - 2 is 0;
    ensure 2 * 2 is 4;
    ensure 2 / 2 is 1;

    ensure [1] & [2] is [1, 2];
    ensure "lead" ~ "tail" is "leadtail";

    ensure "a" x 10 is "aaaaaaaaaa";
    ensure [1] x 10 is [1, 1, 1, 1, 1, 1, 1, 1, 1, 1];
    ensure [1, 2, 3] x 3 is [1, 2, 3, 1, 2, 3, 1, 2, 3];

    # Make sure unexpected binaries evaluate [same way as in runtime]:
    ensure [1] < "12";
    ensure "123" > [1, 2];
    ensure [1, 2] <= [1, 2, 3];
    ensure "123" >= 3;
    ensure "1" < "2" is false;
    ensure "foo" > "barbaz" is false;
    ensure "1" < "hello";
    ensure "barbaz" > "foo";

    ensure "1" + 1 is 2;
    ensure 2 - "2" is 0;
    ensure "2" * ["two", "items"] is 4;
    ensure "2" / true is 2;

    ensure 1 & 2 is [1, 2];
    ensure [1] & 2 is [1, 2];
    ensure 1 & [2] is [1, 2];

    ensure 1 ~ 2 is "12";
    ensure "1" ~ 2 is "12";
    ensure 1 ~ "2" is "12";

    ensure 10 x "a" is "aaaaaaaaaa";
    ensure 10 x [1] is [1, 1, 1, 1, 1, 1, 1, 1, 1, 1];
    ensure 0 x 10 is [0, 0, 0, 0, 0, 0, 0, 0, 0, 0];

    # Make sure we can assign to symbols.
    a = 2;
    b = 3;
    ensure a is 2;
    ensure b is 3;

    # Make sure spread works.
    ensure (|+| [1, 2, 3]) is 6;
    ensure (|*| [1, 2, 3, 4, 5]) is 120;
    ensure |is| 1 x 10;
    ensure not |is| [1, 2, 3];
    ensure (|and| [1, 2, 3]) is 3;
    ensure (|or| [1, 2, 3]) is 1;
    ensure (|and| [true, false, 3]) is false;
    ensure (|and| [false, false, 3]) is false;
    ensure (|or| [false, false, 3]) is 3;
    ensure (|_ + 1| [2, 3, 4]) is [3, 4, 5];
    ensure (|#_| [1, 2, 3]) is 1 x 3;
    ensure (|&_ * _| [2, 3, 4]) is [4, 9, 16];
    ensure (|if (&_ > 5) _ else _ - 1| [4, 9, 16]) is [3, 9, 16];
    ensure (|_| [1 2 3]) is [1 2 3];
    ensure (|&_| [1 2 3]) is [1 2 3];
    ensure (|_ + 1| [1 2 3]) is [2 3 4];
    ensure (|&_ + 1| [1 2 3]) is [2 3 4];
    ensure (|{{{{{_ + 1}}}}}| [1 2 3]) is [2 3 4];
    ensure (|&_ + _| [1 2 3]) is [2 4 6];
    ensure (|&_ + &_| [1 2 3]) is [2 4 6];
    ensure (|if &_ > 1 _| [1 2 3]) is [false 2 3];
    ensure (||_ + 1| _| [[1 2], [3 4]]) is [[2 3], [4 5]];
    ensure (||&_ + 1| _| [[1 2], [3 4]]) is [[2 3], [4 5]];
    ensure (||&_ + 1| &_| [[1 2], [3 4]]) is [[2 3], [4 5]];
    ensure (||&_ + _| &_| [[1 2], [3 4]]) is [[2 4], [6 8]];
    ensure (||&_ + &_| &_| [[1 2], [3 4]]) is [[2 4], [6 8]];
    ensure (||[&_ + &_, _]| &_| [[1 2], [3 4]]) is [[[2 1], [4 2]], [[6 3], [8 4]]];
    ensure (|[|[&_ + &_, _]| &_, _]| [[1 2], [3 4]]) is [[[[2 1], [4 2]], [1 2]], [[[6 3], [8 4]], [3 4]]];

    # Make sure 'if' works.
    ensure (if 1 2 else 3) is 2;
    ensure (if 1 2) is 2;
    ensure (if false 2) is false;
    ensure (if false 2 else 3) is 3;
    ensure (if false _ else 3) dies;
    ensure (if 1 > 2 _ else 3) is 3;
    ensure (if 1 < 2 _ else 3) dies;
    ensure (if 1 is 2 _ else _) dies;
    ensure (if 1 is 1 _ else 3) is 1;

    # Make sure 'dies' works.
    ensure (1 dies) is false;
    ensure (+"123" dies) is false;
    ensure (+"abc" dies) is true;
  }>

  say("All tests passed!");
};

pad(12)
